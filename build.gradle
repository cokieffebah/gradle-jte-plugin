plugins{
    id 'com.gradle.plugin-publish' version '1.0.0'
    id 'groovy'
    id 'org.jenkins-ci.jpi' version '0.43.0'
    id "com.diffplug.gradle.spotless" version "3.29.0"
    id 'codenarc'
}

sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url 'https://repo.jenkins-ci.org/public/' }
    maven { url 'https://repo.jenkins-ci.org/releases/' }
    gradlePluginPortal()
}

version = "0.1.0"
group = "io.jenkins.tools"
description = "Gradle plugin for packaging Jenkins Templating Engine library sources as a Jenkins plugin"

pluginBundle{
    website = "https://github.com/jenkinsci/gradle-jte-plugin"
    vcsUrl = "https://github.com/jenkinsci/gradle-jte-plugin"
    description = project.description
    tags = [ 'jenkins' ]
}

gradlePlugin {
    plugins {
        jte {
            id = 'io.jenkins.jte'
            implementationClass = 'org.jenkinsci.gradle.plugins.jte.JtePlugin'
            displayName = project.description
        }
    }
}

configurations {
    all*.exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    all*.exclude group: "org.junit", module: "junit-bom"
}

jpi.enabled = false
jenkinsPlugin {
    jenkinsVersion = '2.263.1'
}

dependencies {
    implementation gradleApi()
    implementation "org.jenkins-ci.tools:gradle-jpi-plugin:0.43.0"
    implementation 'org.codehaus.groovy:groovy-all:2.4.17'

    // spock dependencies
    testImplementation('org.spockframework:spock-core:1.3-groovy-2.4')
    testImplementation 'cglib:cglib-nodep:3.1'
    testImplementation 'org.objenesis:objenesis:3.1'

    // gradle tests
    testImplementation gradleTestKit()

    // jenkins test dependencies
    testImplementation 'org.jenkins-ci.main:jenkins-test-harness:2.71'
    testImplementation 'org.jenkins-ci.plugins:templating-engine:2.5.2'
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-job:2.42'
    // --> test job dsl
    testImplementation 'org.jenkins-ci.plugins:job-dsl:1.79'
    testImplementation 'org.jenkins-ci.plugins:cloudbees-folder:6.9'
    // --> test JCasC
    testImplementation 'io.jenkins:configuration-as-code:1346.ve8cfa_3473c94'
}

codenarc{
    toolVersion = '1.6.1'
}
codenarcMain{
    source = fileTree("src/main/groovy")
    configFile = file("./.codenarc/main.groovy")
}
codenarcTest {
    source = fileTree("src/test/groovy")
    configFile = file("./.codenarc/test.groovy")
}
task codenarc {
    description = "Evaluate code against CodeNarc"
    dependsOn = [ 'codenarcMain', 'codenarcTest' ]
}


    spotless{
    groovy{
        target = fileTree("src/main/groovy") + fileTree(dir: "src/main/resources", include: "**/*.groovy") + fileTree("src/test/groovy")
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
        licenseHeaderFile 'groovy.header'
    }
}
